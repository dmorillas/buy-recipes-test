plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version '3.5.3'
    id 'io.spring.dependency-management' version '1.1.7'
}

group 'co.morillas'
version '1.0-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.hibernate.orm:hibernate-community-dialects:7.0.3.Final'
    runtimeOnly 'org.xerial:sqlite-jdbc:3.50.1.0'
    
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.assertj:assertj-core:3.27.3'
    testImplementation 'io.rest-assured:rest-assured:5.5.5'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.jacocoTestReport {
    dependsOn tasks.test

    reports {
        xml.required.set(true)
        html.required.set(true)
        csv.required.set(true)
    }

    classDirectories.setFrom(
        files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                "co/morillas/config/**",
                "co/morillas/repository/**",
                "co/morillas/Application.class",
            ])
        })
    )
}
